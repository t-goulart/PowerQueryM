let
    // Define a data inicial fixa para o calendário (1º de janeiro de 2025)
    dataInicialDoCalendario = #date(2025, 1, 1),

    // Obtém a data final do calendário baseada na data atual do sistema
    dataFinalDoCalendario = Date.From(DateTime.LocalNow()),

    // Calcula a quantidade de dias entre a data inicial e a data final
    // adicionando 1 para incluir a data final no intervalo
    // Retorna a diferença de dias entre as datas
    qtdeDias = Duration.Days(dataFinalDoCalendario-dataInicialDoCalendario) + 1,

    // Cria uma lsita de datas com base na data inicial e na quantidade de dias
    listaDatas = List.Dates(dataInicialDoCalendario, qtdeDias, #duration(1, 0, 0, 0)),

    registros = 
        List.Transform(
            listaDatas,
            each
                {
                    _,
                    Text.Proper(Date.DayOfWeekName(_)),
                    Text.Proper(Date.MonthName(_)),
                    Date.Day(_),
                    Date.Month(_),
                    Date.QuarterOfYear(_),
                    Number.ToText(Date.QuarterOfYear(_)) & "° Trimestre",
                    if Date.Month(_) <= 6 then 1 else 2,
                    if Date.Month(_) <= 6 then "1° Semestre" else "2° Semestre",
                    Date.Year(_),
                    #date(Date.Year(_), Date.Month(_), 1),
                    Date.DayOfWeek(_, Day.Sunday),
                    Number.From(Date.ToText(_, "yyyyMM")),
                    Date.ToText(_, "MMyyyy"),
                    if DateTime.Date(Date.StartOfMonth(DateTime.LocalNow())) = Date.StartOfMonth(_) then "MÊS ATUAL" else Date.ToText(_, "MMyyyy")
                }
        ),

    tabelaCalendario =
        #table(
            type 
                table[
                    Data = date, 
                    Dia Semana = text, 
                    Nome Mês = text,
                    Dia = Int64.Type,
                    Mês = Int64.Type,
                    Trimestre = Int64.Type,
                    Nome Trimestre = text,
                    Semestre = Int64.Type,
                    Nome Semestre = text,
                    Ano = Int64.Type,
                    Safra = date,
                    Dia útil = Int64.Type, 
                    Ano Mês = Int64.Type,
                    Mês Ano = text,
                    Mês Atual = text
                ],
            registros
        ),

    ultimoSemestre = Table.AddColumn(
        tabelaCalendario, 
        "Ultimo Semestre", each 
            let
                DataAtual = DateTime.Date(DateTime.FixedLocalNow()), // Obtém a data e hora atuais
                SeteMesesAtras = Date.StartOfMonth(Date.AddMonths(DataAtual, -6)), // Calcula a data de 7 meses atrás
                UltimoDiaMesAtual = Date.EndOfMonth(DataAtual),
                Resultado = if [Data] >= SeteMesesAtras and [Data] <= UltimoDiaMesAtual then "Sim" else "Não" // Verifica se a data está nos últimos 7 meses
            in
                Resultado,
        type text
    )
in
   ultimoSemestre
