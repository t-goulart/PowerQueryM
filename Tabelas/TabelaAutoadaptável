let
    // Conecta ao arquivo Excel utilizando uma variável de caminho e lê o conteúdo binário
    Fonte = Excel.Workbook(File.Contents(varCaminho & "\Rendimentos.xlsx"), null, true),

    // Filtra o objeto desejado dentro do arquivo (neste caso, a aba "Planilha1") para garantir que não leremos tabelas ou áreas nomeadas indesejadas
    filtraLinhas = Table.SelectRows(Fonte, each ([Kind] = "Sheet") and ([Item] = "Planilha1")),

    listaConlunasDinamicas = List.Distinct( // Cria uma lista distinguindo o nome das colunas para evitar duplicados e travar o código
        List.Combine( // Combina o resultado gerando uma lista com o nome de todas as colunas (Assim premeditamos até a união de multiplos arquivos)
            List.Transform( // Vai percorrer cada uma das linhas dentro da coluna Data para capturarmos o nome das colunas
                filtraLinhas[Data], // Procura o nome das colunas que vamos extrair dentro do Data antes de expandir os dados
                each Table.ColumnNames(_) // Identifica o nome de cada uma das colunas
            )
        )
    ),

    // Expande o conteúdo das tabelas aninhadas na coluna Data utilizando a lista dinâmica gerada anteriormente para evitar erro de colunas faltantes
    dadosExpandidos = Table.ExpandTableColumn(
        filtraLinhas, // Etapa anterior 
        "Data", // Nome da coluna que contém as tabelas (nomes das colunas que vamos identificar automaticamente e expandir)
        listaConlunasDinamicas, // Nome das colunas que queremos extrair da coluna Data
        listaConlunasDinamicas // Como queremos nomear cada uma das novas colunas (Vai ser o padrão Column1, Column2, etc..)
    ),

    removeColunasNaoUtilizadas = Table.RemoveColumns( // Remove as colunas desnecessárias
        dadosExpandidos, // Etapa anterior
        {
            "Name",
            "Item",
            "Kind",
            "Hidden"
        },
        MissingField.Ignore // Se uma das colunas desaparecer não vai travar o código
    ),

    // Eleva a primeira linha de dados ao status de cabeçalho, convertendo todos os tipos de escalares para texto temporariamente
    promoveCabecalhos = Table.PromoteHeaders(
        removeColunasNaoUtilizadas, 
        [PromoteAllScalars=true]
    ),

    // Esta etapa força o Power Query a analisar o conteúdo de cada coluna 
    // e decidir o melhor tipo (Data, Número, Texto) de forma 100% dinâmica.
    definicaoDadosAutomatica = Table.TransformColumnTypes(
        promoveCabecalhos, 
        List.Transform( // Vai percorrer cada uma das colunas que estamos informando na próxima linha
            Table.ColumnNames(promoveCabecalhos), // Indentifica exclusivamente o nome das colunas que vamos usar nas transformações a seguir
            each // Itera sobre cada uma das colunas (Le o nome de cada coluna a cada "volta" como se fosse um for each)
            { 
                _, // Passa a coluna como um parametro (Esse underline é o nome da coluna) 
                Value.Type( // Identifica o tipo, ou seja, se encontrar o valor 50, ela retorna Int64.Type. Se encontrar 01/01/2025, retorna Date.Type.
                    Record.Field( // Vai até a primeira linha e captura o valor da coluna
                        Table.FirstN(promoveCabecalhos, 1){0}, // Isola apenas a primeira linha da tabela e analisa o tipo de dado para definir os dados da coluna | Esse zero representa a primeira linha
                        _ // Passa o nome da coluna como parametro novamente
                    )
                )
            }
        )
    )
in
    definicaoDadosAutomatica
