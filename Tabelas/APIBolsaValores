let
    // Essa tabela procura e retorna o valor das cotações de ações e fundos imobiliarios
    // Definição da Função de Consulta à API
    // Adicionado tratamento de erro robusto e tipagem de entrada
    _consultaCotacao = (Ticker as text) as any =>
        let
            Token = "", // Informe aqui o seu token gerado no site da BRAPI (É gratuito!)
            UrlBase = "https://brapi.dev/api/quote/", // URL onde vamos consultar cada um dos códigos da bolsa de valores
            
            // Realiza a chamada Web. Usamos Function.InvokeAfter para dar um "fôlego" à API
            // Isso ajuda a evitar o erro 429 em listas grandes
            ChamadaApi = Function.InvokeAfter( // Controla o tempo da requisição. Em vez de executar imediatamente, força o Power Query a esperar um intervalo específico que definimos na ultima etapa
                () => Web.Contents( // Gera uma função anônima que executa o Web.Contentes após o tempo de espera que determinamos na ultima etapa do Function.InvokeAfter 
                    UrlBase & Ticker, // Concatena o endereço da API com o código da ação (ex: https://api.com/MXRF11).
                    [
                        Query = [token = Token], // Limpa os parametros na URL a cada tentativa de consulta 
                        ManualStatusHandling = {404, 429, 500} // Instrui o Power Query a não interromper o código imediatamente se encontrar erros como 429 (Too Many Requests) ou 500 (Erro de Servidor)
                    ]
                ),
                #duration(0, 0, 0, 0.1) // 100ms de intervalo entre requisições
            ),
            
            // Converte o resultado para JSON
            Json = Json.Document(ChamadaApi), // É o mesmo que usamos para consultar dados JSON
            
            // Extrai o preço navegando na estrutura: results -> primeiro item -> regularMarketPrice
            // Usamos try para o caso de o Ticker não ser encontrado ou o JSON vir vazio
            Preco = try Json[results]{0}[regularMarketPrice] otherwise null
        in
            Preco,

    // Referência à tabela de empresas (dEmpresa)
    fonte = Table.SelectRows(
        dEmpresas, // Tabela que contém o código das empresas que a API vai consultar (Crie uma tabela auxiliar ou adicione esse codigo a sua tabela)
        each // Itera sobre cada uma das linhas da tabela dEmpresas procurando apenas por Ações e FII 
            Text.Contains([Tipo de Investimento], "Ações") or 
            Text.Contains([Tipo de Investimento], "FII")
    ),

    // Execução da iteração (O "For Each" do Power Query)
    // Criamos a coluna chamando a função para cada linha
    AdicionarCotacao = Table.AddColumn(
        fonte, // Etapa anterior 
        "Preço Atual", // Nome da coluna
        each // Itera (Le linha a linha 
            if [Código] <> null then // Se o código é diferente de nulo 
                try _consultaCotacao([Código]) otherwise null // try (Tenta) aplicar a função passando como parametro o código da ação/fii 
            else null, // Se o código é nulo retorna nulo
        type number // Define o tipo de dado como numérico com virgula
    )
in
    AdicionarCotacao
