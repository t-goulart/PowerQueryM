let
    // Definição da Função de Consulta à API
    // Adicionado tratamento de erro robusto e tipagem de entrada
    _consultaCotacao = (Ticker as text) as any =>
        let
            Token = "", // Informe aqui o seu token gerado no site da BRAPI (É gratuito!)
            UrlBase = "https://brapi.dev/api/quote/", // URL onde vamos consultar cada um dos códigos da bolsa de valores
            
            // Realiza a chamada Web. Usamos Function.InvokeAfter para dar um "fôlego" à API
            // Isso ajuda a evitar o erro 429 em listas grandes
            _chamadaApi = Function.InvokeAfter( // O Function.InvokeAfter pede para o Power Query aguardar um pouco antes de rodar o código a seguir
                () => Web.Contents( // Estamos pedindo para o Power Query fazer uma consulta em um sit
                    UrlBase & Ticker, // Junta o endereço com o nome da ação (ex: https://brapi.dev/api/quote/MXRF11).
                    [
                        Query = [ // Adiciona os parâmetros após a interrogação da URL
                            token = Token,
                            fundamental = "true" // O fundamental=true é o comando que avisa à API: "não me dê só o preço, me dê os indicadores também
                        ],
                        ManualStatusHandling = {404, 429, 500} // Evita que o Power BI exiba uma mensagem de erro vermelha e pare tudo caso o site caia (500) ou a ação não exista 404 ou 429, permitindo que o erro aconteça "silenciosamente"
                    ]
                ),
                #duration(0, 0, 0, 0.1) // Define o tempo de espera. São 0.1 segundos (100 milissegundos) entre cada linha da tabela
            ),
            
            _Json = Json.Document(_chamadaApi), // A API retorna os dados em formato JSON
            
            // Em vez de retornar apenas o Preço, retornamos o registro [0] inteiro
            _Dados = try _Json[results]{0} otherwise null //Passa para a variável os dados que estão dentro da lista results
        in
            _Dados,

    // Referência à tabela de empresas (dEmpresa)
    fonte = Table.SelectRows(
        dEmpresas, // Tabela que contém o código das empresas que a API vai consultar
        each // Itera sobre cada uma das linhas da tabela dEmpresas procurando apenas por Ações e FII 
            Text.Contains([Tipo de Investimento], "Ações") or 
            Text.Contains([Tipo de Investimento], "FII")
    ),

    adicionarDadosBrutos = Table.AddColumn(
        fonte, 
        "API", 
        each 
            if [Código] <> null then // Se o código é diferen de nulo entra na primeira condição
                try _consultaCotacao([Código]) otherwise null // O try e o otherwise são tentativas de aplicar a função, se ocorrer erro retorna nulo
            else 
                null
    ),

    // Expandir as colunas desejadas
    // Aqui você escolhe o que quer ver. Ex: regularMarketPrice, priceEarnings, averageDailyVolume3Month, etc.
    expandirColunas = Table.ExpandRecordColumn(
        adicionarDadosBrutos, // Etapa anterior 
        "API", 
        {
            "regularMarketPrice", // O preço atual (última cotação) pelo qual o ativo foi negociado (R$ 64,63)
            "regularMarketChange", // A variação em valor nominal em relação ao fechamento anterior (-0,29 centavos)
            "regularMarketChangePercent", // A variação percentual do dia (-0,447%). 
            "marketCap",            // Valor Total da Entidade (Mercado)
            "priceToBook",          // P/VP (Ajuda a calcular o Patrimonial)
            "regularMarketVolume"   // Volume/liquidez do dia
        }, 
        {
            "Preço Atual",
            "Variação Nominal",
            "Variação Percentual do dia", 
            "Valor de Mercado", 
            "P/VP",
            "Volume/Liquidez"
        }
    ),

   // Esta etapa força o Power Query a analisar o conteúdo de cada coluna 
    // e decidir o melhor tipo (Data, Número, Texto) de forma 100% dinâmica.
    definicaoDadosAutomatica = Table.TransformColumnTypes( // Transforma o tipo de dado da coluna ou colunas
        expandirColunas, // Etapa anterior 
        List.Transform( // Funciona como um laço de repetição iterando sobre a lista que é passada
            Table.ColumnNames(expandirColunas), // Captura o nome das colunas que foram identificadas até a etapa informada (removeColunasNaoUtilizadas) fazendo o List.Transform iterar sobre as colunas
            each // O each vai funcionar como um laço de repetição lendo cada uma das colunas 
                let
                    _valoresAmostra = List.FirstN( // Lista os primeiro "X" valores
                        Table.Column(expandirColunas, _), // Com base na estrutura construída até a etapa removeColunasNaoUtilizadas, pega a coluna atual _ (underline é o nome da coluna atual)
                        10 // Vai fazer com que o List.FirstN capture os 10 primeiros valores
                    ), 

                    _primeiroValorValido = List.First( // Obtém o primeiro valor
                        List.RemoveNulls(_valoresAmostra) // Removemos todos os valores nulos para que facilite na identificação do tipo de dado
                    ), 
                    
                    _tipoIdentificado = 
                        if _primeiroValorValido = null then type text // SE for nulo (coluna vazia), define como 'type text', mas você pode alterar para outro tipo ou any
                        else Value.Type(_primeiroValorValido) // SENÃO, identifica o tipo do valor encontrado.
                in
                    { _, _tipoIdentificado} // Converter o valor da coluna _ (Nome da coluna) para o valor identificado com base nas primeiras "x" linhas
        )
    )
in
    definicaoDadosAutomatica
